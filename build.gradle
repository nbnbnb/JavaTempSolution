// https://docs.gradle.org/current/userguide/plugins.html
plugins {
    // 打包 jar 包（SpringBoot 插件自带此功能）
    // https://plugins.gradle.org/plugin/com.github.johnrengelman.shadow
    // BasicConsoleApp 和 Java8ConsoleApp 使用此插件打包完整 jar
    id "com.github.johnrengelman.shadow" version "4.0.4" apply false
    // https://plugins.gradle.org/plugin/org.springframework.boot
    id "org.springframework.boot" version "2.1.3.RELEASE" apply false
    // https://plugins.gradle.org/plugin/org.jetbrains.kotlin.plugin.noarg
    id "org.jetbrains.kotlin.plugin.noarg" version "1.3.21" apply false
    // https://plugins.gradle.org/plugin/org.jetbrains.kotlin.plugin.allopen
    id "org.jetbrains.kotlin.plugin.allopen" version "1.3.21" apply false
    // https://plugins.gradle.org/plugin/org.jetbrains.kotlin.jvm
    // https://kotlinlang.org/docs/reference/using-gradle.html
    id 'org.jetbrains.kotlin.jvm' version '1.3.21' apply false
}


subprojects {

    if (name == 'BasicConsoleApp') {
        apply plugin: 'com.github.johnrengelman.shadow'
    }

    if (name == 'Java8ConsoleApp') {
        apply plugin: 'com.github.johnrengelman.shadow'
    }

    // 如果有 Java 代码，则需要此插件
    apply plugin: 'java'

    // 将 kotlin 编译为 JVM 平台代码
    apply plugin: 'kotlin'

    // 使用 maven 仓库
    apply plugin: 'maven'

    // A Gradle plugin that provides Maven-like dependency management functionality
    // https://github.com/spring-gradle-plugins/dependency-management-plugin
    apply plugin: 'io.spring.dependency-management'

    apply plugin: "kotlin-allopen"
    apply plugin: 'kotlin-spring'

    apply plugin: "kotlin-noarg"
    apply plugin: "kotlin-jpa"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    group = 'me.zhangjin.study'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        // mavenCentral()
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = '1.8'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = '1.8'
        }
    }

    dependencies {
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib'
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'
        compile('com.fasterxml.jackson.module:jackson-module-kotlin')
        compile group: 'org.springframework', name: 'spring-context'
        compile "io.reactivex.rxjava2:rxjava"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:${kotlinx_version}"

        testCompile group: 'org.springframework', name: 'spring-test'
        testCompile "org.jetbrains.kotlin:kotlin-test"
        testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit'

    }

    dependencyManagement {
        imports {
            // 导入 Spring Boot POM 依赖
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBoot_version}"
        }
    }
}
